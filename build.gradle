import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import java.util.concurrent.TimeUnit

buildscript {
    repositories {
        maven {
            name = "jitpack"
            url = "https://jitpack.io"
        }
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'com.github.GTNH2:ForgeGradle:FG_1.2-SNAPSHOT'
    }
}

plugins {
    id("org.ajoberstar.grgit") version("3.1.1")
    id("com.github.johnrengelman.shadow") version("4.0.4")
    id("com.palantir.git-version") version("0.12.3")
}

apply plugin: 'forge'
apply plugin: 'idea'

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor(0, TimeUnit.SECONDS)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Pulls version from git tag
version = minecraftVersion + "-" + gitVersion()
group = modGroup
archivesBaseName = modId

minecraft {
    version = minecraftVersion + "-" + forgeVersion + "-" + minecraftVersion
    runDir = "run"

    replaceIn "src/main/java/com/sinthoras/visualprospecting/Tags.java"
    replace "GRADLETOKEN_VERSION", version
}

repositories {
    maven {
        name = "CodeChicken"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/repository/maven-public"
    }
}

configurations {
    implementation.extendsFrom(shadowImplementation)
}

dependencies {
    annotationProcessor("org.ow2.asm:asm-debug-all:5.0.3")
    annotationProcessor("com.google.guava:guava:24.1.1-jre")
    annotationProcessor("com.google.code.gson:gson:2.8.6")
    annotationProcessor("org.spongepowered:mixin:0.8-SNAPSHOT") // using 0.8 to workaround a issue in 0.7 which fails mixin application
    compile("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        // Mixin includes a lot of dependencies that are too up-to-date
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
        exclude module: "log4j-core"
    }
    compile("com.github.GTNewHorizons:SpongeMixins:1.3.3:dev")

    shadowImplementation("com.github.SinTh0r4s:Enklume:master-SNAPSHOT")

    compile("com.github.GTNewHorizons:GT5-Unofficial:experimental-SNAPSHOT:dev") {
        transitive = false
        setChanging(true)
    }

    // GT5U Dependencies
    compile("net.industrial-craft:industrialcraft-2:2.2.828-experimental:dev")
    compile("com.github.GTNewHorizons:StructureLib:1.0.6:deobf")
    compile("codechicken:CodeChickenLib:1.7.10-1.1.3.140:dev")
    compile("codechicken:CodeChickenCore:1.7.10-1.0.7.47:dev")
    compile("codechicken:NotEnoughItems:1.7.10-1.0.5.120:dev")

    compile("curse.maven:journeymap-32274:2367915")

    compile("com.github.GTNewHorizons:TCNodeTracker:latest")

    compileOnly("com.github.GTNewHorizons:bartworks:0.5.24") {
        transitive = false
    }

    compileOnly("com.github.GTNewHorizons:GalacticGregGT5:1.0.3") {
        transitive = false
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "com.sinthoras.visualprospecting.shadow"
}

def mixinConfigJson = "mixins." + modId + ".json"
def mixingConfigRefMap = "mixins." + modId + ".refmap.json"
def refMap = "${tasks.compileJava.temporaryDir}" + File.separator + mixingConfigRefMap
def mixinSrg = "${tasks.reobf.temporaryDir}" + File.separator + "mixins.srg"

shadowJar {
    from refMap
    manifest {
        attributes([
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs"               : mixinConfigJson,
                "FMLCorePluginContainsFMLMod": true,
                "ForceLoadAsMod"             : true
        ])
    }
    minimize()
    configurations = [project.configurations.shadowImplementation]
    dependsOn(relocateShadowJar)
}

jar {
    dependsOn(shadowJar)
    enabled = false
}

reobf {
    addExtraSrgFile mixinSrg
}

afterEvaluate {
    tasks.compileJava {
        options.compilerArgs += [
                "-AreobfSrgFile=${tasks.reobf.srg}",
                "-AoutSrgFile=${mixinSrg}",
                "-AoutRefMapFile=${refMap}",
                // Elan: from what I understand they are just some linter configs so you get some warning on how to properly code
                "-Xlint:-sunapi",
                "-XDenableSunApiLintControl",
                "-XDignore.symbol.file"
        ]
    }
}

runClient {
    def playerUserName = System.env.PLAYER_USER_NAME
    if (playerUserName == null)  {
        playerUserName = "Player-default"
    }

    args    "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
            // Having mixin in the same jar as normal mode makes FML ignore it.
            // It should be fine in production, however I suppose it's not properly read here.
            // The only option which worked for me was adding it as a mod explicitly via next argument:
            "--mods=../build/libs/$modId-${version}.jar",
            "--username", playerUserName
}

runServer {
    args    "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
            // Having mixin in the same jar as normal mode makes FML ignore it.
            // It should be fine in production, however I suppose it's not properly read here.
            // The only option which worked for me was adding it as a mod explicitly via next argument:
            "--mods=../build/libs/$modId-${version}.jar"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand "mcversion": project.minecraft.version,
                "version": version,
                "modid": modId,
                "modName": modName
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar) {
    from (sourceSets.main.allJava)
    from (file("$projectDir/LICENSE"))

    getArchiveClassifier().set('sources')
}

task devJar(type: Jar) {
    from sourceSets.main.output
    archiveClassifier.set("dev")
}


artifacts {
    archives sourcesJar
    archives devJar
}
